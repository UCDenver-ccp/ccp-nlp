/* ****************************************************************

   Copyright (C) 2004 Burr Settles, University of Wisconsin-Madison,
   Dept. of Computer Sciences and Dept. of Biostatistics and Medical
   Informatics.

   This file is part of the "ABNER (A Biomedical Named Entity
   Recognizer)" system. It requires Java 1.4. This software is
   provided "as is," and the author makes no representations or
   warranties, express or implied. For details, see the "README" file
   included in this distribution.

   This software is provided under the terms of the Common Public
   License, v1.0, as published by http://www.opensource.org. For more
   information, see the "LICENSE" file included in this distribution.

   **************************************************************** */

package abner;

import java.lang.*;
import java.io.*;

/**
   <p>ABNER's Scanner class implements the finite state machine used
   in tokenization.

   @author Burr Settles <a href="http://www.cs.wisc.edu/~bsettles">bsettles&#64;&#99;s&#46;&#119;i&#115;&#99;&#46;&#101;d&#117;</a> 
   @version 1.5 (March 2005)
*/

%%

%full
%notunix
%public
%class Scanner
%function nextToken
%type String

CAPS= [A-Z]
ALPHA= [A-Za-z]
DIGIT= [0-9]
WHITESPACE= [\ \t]
NEWLINE= [\r\n]

%% 

{NEWLINE}{NEWLINE}+ { return "\n"; }
{NEWLINE} {}
{WHITESPACE}+ {}

("vs."|"i.e."|"e.g."|"et."|"al."|"a.m."|"p.m."|"ca."|"pp.") { return (yytext()); }
("Dr."|"Mr."|"Mrs."|"Jr."|"Sr.") { return (yytext()); }
("Vol."|"vol."|"Iss."|"iss."|"Surg."|"Hematol."|"Oncol."|"Blood."|"Neurosci."|"Med."|"Cell."|"Virol."|"Rheum."|"Pharmacol."|"Nutr."|"Nat."|"Biol."|"Chem."|"Mol."|"Jour."|"Exp."|"Res."|"Am."|"Physiol."|"Inflamm."|"Immunol.") { return (yytext()); }

("d'"|"'s"|"'t"|"'ll"|"n't"|"'m"|"'re") { return (yytext()); }

("-"|"_"|{ALPHA})+(({DIGIT}|{CAPS})+)?("+")? { return (yytext()); }
{ALPHA}+{DIGIT}+("/"){DIGIT}+ { return (yytext()); }

"-"?({ALPHA}|{DIGIT}|"_")+("."({ALPHA}|{DIGIT}))+ { return (yytext()); }
("-"?{CAPS}".")+ { return (yytext()); }

{CAPS}"'" { return (yytext()); }
{DIGIT}"'" { return (yytext()); }
{DIGIT}"+" { return (yytext()); }
"-"?{DIGIT}+"%"? { return (yytext()); }
"-"?{DIGIT}+(("."|","|"/"|"-"){DIGIT}+)+"%"? { return (yytext()); }
"."{DIGIT}+ { return (yytext()); }

"http://"({ALPHA}|{DIGIT}|"/"|","|"."|"_"|"-")+  { return (yytext()); }
({ALPHA}|{DIGIT}|"-"|".")+"@"({ALPHA}|{DIGIT}|"-"|".")+({ALPHA}|{DIGIT}) { return (yytext()); }

".""."+ { return (yytext()); }

<YYINITIAL> "\"" { return (yytext()); }
<YYINITIAL> "'" { return (yytext()); }
<YYINITIAL> "`" { return (yytext()); }
<YYINITIAL> "''" { return "\""; }
<YYINITIAL> "``" { return "\""; }

<YYINITIAL> "," { return (yytext()); }
<YYINITIAL> ":"+ { return (yytext()); }
<YYINITIAL> ";" { return (yytext()); }
<YYINITIAL> "." { return (yytext()); }
<YYINITIAL> ("?"|"!")+ { return (yytext()); }

<YYINITIAL> "(" { return (yytext()); }
<YYINITIAL> ")" { return (yytext()); }
<YYINITIAL> "[" { return (yytext()); }
<YYINITIAL> "]" { return (yytext()); }
<YYINITIAL> "{" { return (yytext()); }
<YYINITIAL> "}" { return (yytext()); }

<YYINITIAL> "+" { return (yytext()); }
<YYINITIAL> "*" { return (yytext()); }
<YYINITIAL> "/" { return (yytext()); }
<YYINITIAL> "=" { return (yytext()); }

<YYINITIAL> "<>" { return (yytext()); }
<YYINITIAL> "<"  { return (yytext()); }
<YYINITIAL> "<=" { return (yytext()); }
<YYINITIAL> ">"  { return (yytext()); }
<YYINITIAL> ">=" { return (yytext()); }

<YYINITIAL> "&"  { return (yytext()); }
<YYINITIAL> "|"  { return "/"; }

. { return (yytext()); }
